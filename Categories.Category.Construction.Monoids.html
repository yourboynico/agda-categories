<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Category.Construction.Monoids</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>
<a id="36" class="Keyword">open</a> <a id="41" class="Keyword">import</a> <a id="48" href="Categories.Category.Core.html" class="Module">Categories.Category.Core</a>
<a id="73" class="Keyword">open</a> <a id="78" class="Keyword">import</a> <a id="85" href="Categories.Category.Monoidal.Core.html" class="Module">Categories.Category.Monoidal.Core</a>

<a id="120" class="Comment">-- This module defines the category of monoids internal to a given monoidal</a>
<a id="196" class="Comment">-- category.</a>

<a id="210" class="Keyword">module</a> <a id="217" href="Categories.Category.Construction.Monoids.html" class="Module">Categories.Category.Construction.Monoids</a> <a id="258" class="Symbol">{</a><a id="259" href="Categories.Category.Construction.Monoids.html#259" class="Bound">o</a> <a id="261" href="Categories.Category.Construction.Monoids.html#261" class="Bound">ℓ</a> <a id="263" href="Categories.Category.Construction.Monoids.html#263" class="Bound">e</a><a id="264" class="Symbol">}</a> <a id="266" class="Symbol">{</a><a id="267" href="Categories.Category.Construction.Monoids.html#267" class="Bound">𝒞</a> <a id="269" class="Symbol">:</a> <a id="271" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="280" href="Categories.Category.Construction.Monoids.html#259" class="Bound">o</a> <a id="282" href="Categories.Category.Construction.Monoids.html#261" class="Bound">ℓ</a> <a id="284" href="Categories.Category.Construction.Monoids.html#263" class="Bound">e</a><a id="285" class="Symbol">}</a> <a id="287" class="Symbol">(</a><a id="288" href="Categories.Category.Construction.Monoids.html#288" class="Bound">C</a> <a id="290" class="Symbol">:</a> <a id="292" href="Categories.Category.Monoidal.Core.html#1287" class="Record">Monoidal</a> <a id="301" href="Categories.Category.Construction.Monoids.html#267" class="Bound">𝒞</a><a id="302" class="Symbol">)</a> <a id="304" class="Keyword">where</a>

<a id="311" class="Keyword">open</a> <a id="316" class="Keyword">import</a> <a id="323" href="Level.html" class="Module">Level</a>

<a id="330" class="Keyword">open</a> <a id="335" class="Keyword">import</a> <a id="342" href="Categories.Functor.html" class="Module">Categories.Functor</a> <a id="361" class="Keyword">using</a> <a id="367" class="Symbol">(</a><a id="368" href="Categories.Functor.Core.html#248" class="Record">Functor</a><a id="375" class="Symbol">)</a>
<a id="377" class="Keyword">open</a> <a id="382" class="Keyword">import</a> <a id="389" href="Categories.Morphism.Reasoning.html" class="Module">Categories.Morphism.Reasoning</a> <a id="419" href="Categories.Category.Construction.Monoids.html#267" class="Bound">𝒞</a>
<a id="421" class="Keyword">open</a> <a id="426" class="Keyword">import</a> <a id="433" href="Categories.Object.Monoid.html" class="Module">Categories.Object.Monoid</a> <a id="458" href="Categories.Category.Construction.Monoids.html#288" class="Bound">C</a>

<a id="461" class="Keyword">open</a> <a id="466" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="475" href="Categories.Category.Construction.Monoids.html#267" class="Bound">𝒞</a>
<a id="477" class="Keyword">open</a> <a id="482" href="Categories.Category.Monoidal.Core.html#1287" class="Module">Monoidal</a> <a id="491" href="Categories.Category.Construction.Monoids.html#288" class="Bound">C</a>
<a id="493" class="Keyword">open</a> <a id="498" href="Categories.Category.Core.html#2462" class="Module">HomReasoning</a>
<a id="511" class="Keyword">open</a> <a id="516" href="Categories.Object.Monoid.html#623" class="Module">Monoid</a> <a id="523" class="Keyword">using</a> <a id="529" class="Symbol">(</a><a id="530" href="Categories.Object.Monoid.html#451" class="Function">η</a><a id="531" class="Symbol">;</a> <a id="533" href="Categories.Object.Monoid.html#432" class="Function">μ</a><a id="534" class="Symbol">)</a>
<a id="536" class="Keyword">open</a> <a id="541" href="Categories.Object.Monoid.html#766" class="Module">Monoid⇒</a>

<a id="Monoids"></a><a id="550" href="Categories.Category.Construction.Monoids.html#550" class="Function">Monoids</a> <a id="558" class="Symbol">:</a> <a id="560" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="569" class="Symbol">(</a><a id="570" href="Categories.Category.Construction.Monoids.html#259" class="Bound">o</a> <a id="572" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="574" href="Categories.Category.Construction.Monoids.html#261" class="Bound">ℓ</a> <a id="576" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="578" href="Categories.Category.Construction.Monoids.html#263" class="Bound">e</a><a id="579" class="Symbol">)</a> <a id="581" class="Symbol">(</a><a id="582" href="Categories.Category.Construction.Monoids.html#261" class="Bound">ℓ</a> <a id="584" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="586" href="Categories.Category.Construction.Monoids.html#263" class="Bound">e</a><a id="587" class="Symbol">)</a> <a id="589" href="Categories.Category.Construction.Monoids.html#263" class="Bound">e</a>
<a id="591" href="Categories.Category.Construction.Monoids.html#550" class="Function">Monoids</a> <a id="599" class="Symbol">=</a> <a id="601" class="Keyword">record</a>
  <a id="610" class="Symbol">{</a> <a id="612" href="Categories.Category.Core.html#559" class="Field">Obj</a> <a id="616" class="Symbol">=</a> <a id="618" href="Categories.Object.Monoid.html#623" class="Record">Monoid</a>
  <a id="627" class="Symbol">;</a> <a id="629" href="Categories.Category.Core.html#575" class="Field Operator">_⇒_</a> <a id="633" class="Symbol">=</a> <a id="635" href="Categories.Object.Monoid.html#766" class="Record">Monoid⇒</a>
  <a id="645" class="Symbol">;</a> <a id="647" href="Categories.Category.Core.html#595" class="Field Operator">_≈_</a> <a id="651" class="Symbol">=</a> <a id="653" class="Symbol">λ</a> <a id="655" href="Categories.Category.Construction.Monoids.html#655" class="Bound">f</a> <a id="657" href="Categories.Category.Construction.Monoids.html#657" class="Bound">g</a> <a id="659" class="Symbol">→</a> <a id="661" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="665" href="Categories.Category.Construction.Monoids.html#655" class="Bound">f</a> <a id="667" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="669" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="673" href="Categories.Category.Construction.Monoids.html#657" class="Bound">g</a>
  <a id="677" class="Symbol">;</a> <a id="679" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="682" class="Symbol">=</a> <a id="684" class="Keyword">record</a>
    <a id="695" class="Symbol">{</a> <a id="697" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="701" class="Symbol">=</a> <a id="703" href="Categories.Category.Core.html#630" class="Function">id</a>
    <a id="710" class="Symbol">;</a> <a id="712" href="Categories.Object.Monoid.html#855" class="Field">preserves-μ</a> <a id="724" class="Symbol">=</a> <a id="726" href="Categories.Category.Core.html#1096" class="Function">identityˡ</a> <a id="736" href="Categories.Category.Core.html#3061" class="Function Operator">○</a> <a id="738" href="Categories.Morphism.Reasoning.Core.html#2225" class="Function">introʳ</a> <a id="745" class="Symbol">(</a><a id="746" href="Categories.Functor.Core.html#511" class="Field">Functor.identity</a> <a id="763" href="Categories.Category.Monoidal.Core.html#1355" class="Field">⊗</a><a id="764" class="Symbol">)</a>
    <a id="770" class="Symbol">;</a> <a id="772" href="Categories.Object.Monoid.html#903" class="Field">preserves-η</a> <a id="784" class="Symbol">=</a> <a id="786" href="Categories.Category.Core.html#1096" class="Function">identityˡ</a>
    <a id="800" class="Symbol">}</a>
  <a id="804" class="Symbol">;</a> <a id="806" href="Categories.Category.Core.html#656" class="Field Operator">_∘_</a> <a id="810" class="Symbol">=</a> <a id="812" class="Symbol">λ</a> <a id="814" class="Symbol">{</a><a id="815" href="Categories.Category.Construction.Monoids.html#815" class="Bound">A</a> <a id="817" href="Categories.Category.Construction.Monoids.html#817" class="Bound">B</a> <a id="819" href="Categories.Category.Construction.Monoids.html#819" class="Bound">C</a><a id="820" class="Symbol">}</a> <a id="822" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="824" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a> <a id="826" class="Symbol">→</a> <a id="828" class="Keyword">record</a>
    <a id="839" class="Symbol">{</a> <a id="841" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="845" class="Symbol">=</a> <a id="847" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="851" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="853" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="855" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="859" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a>
    <a id="865" class="Symbol">;</a> <a id="867" href="Categories.Object.Monoid.html#855" class="Field">preserves-μ</a> <a id="879" class="Symbol">=</a> <a id="881" href="Relation.Binary.Reasoning.Base.Single.html#1916" class="Function Operator">begin</a>
      <a id="893" class="Symbol">(</a><a id="894" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="898" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="900" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="902" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="906" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="907" class="Symbol">)</a> <a id="909" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="911" href="Categories.Object.Monoid.html#432" class="Function">μ</a> <a id="913" href="Categories.Category.Construction.Monoids.html#815" class="Bound">A</a>                    <a id="934" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="937" href="Categories.Morphism.Reasoning.Core.html#1453" class="Function">pullʳ</a> <a id="943" class="Symbol">(</a><a id="944" href="Categories.Object.Monoid.html#855" class="Field">preserves-μ</a> <a id="956" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="957" class="Symbol">)</a> <a id="959" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
      <a id="967" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="971" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="973" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="975" class="Symbol">(</a><a id="976" href="Categories.Object.Monoid.html#432" class="Function">μ</a> <a id="978" href="Categories.Category.Construction.Monoids.html#817" class="Bound">B</a> <a id="980" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="982" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="986" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a> <a id="988" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="991" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="995" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="996" class="Symbol">)</a>           <a id="1008" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1011" href="Categories.Morphism.Reasoning.Core.html#1587" class="Function">pullˡ</a> <a id="1017" class="Symbol">(</a><a id="1018" href="Categories.Object.Monoid.html#855" class="Field">preserves-μ</a> <a id="1030" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a><a id="1031" class="Symbol">)</a> <a id="1033" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
      <a id="1041" class="Symbol">(</a><a id="1042" href="Categories.Object.Monoid.html#432" class="Function">μ</a> <a id="1044" href="Categories.Category.Construction.Monoids.html#819" class="Bound">C</a> <a id="1046" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1048" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1052" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="1054" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1057" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1061" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a><a id="1062" class="Symbol">)</a> <a id="1064" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1066" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1070" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a> <a id="1072" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1075" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1079" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a>  <a id="1082" href="Relation.Binary.Reasoning.Setoid.html#1153" class="Function">≈˘⟨</a> <a id="1086" href="Categories.Morphism.Reasoning.Core.html#1781" class="Function">pushʳ</a> <a id="1092" class="Symbol">(</a><a id="1093" href="Categories.Functor.Core.html#565" class="Field">Functor.homomorphism</a> <a id="1114" href="Categories.Category.Monoidal.Core.html#1355" class="Field">⊗</a><a id="1115" class="Symbol">)</a> <a id="1117" href="Relation.Binary.Reasoning.Setoid.html#1153" class="Function">⟩</a>
      <a id="1125" href="Categories.Object.Monoid.html#432" class="Function">μ</a> <a id="1127" href="Categories.Category.Construction.Monoids.html#819" class="Bound">C</a> <a id="1129" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1131" class="Symbol">(</a><a id="1132" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1136" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="1138" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1140" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1144" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="1145" class="Symbol">)</a> <a id="1147" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1150" class="Symbol">(</a><a id="1151" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1155" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a> <a id="1157" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1159" href="Categories.Object.Monoid.html#822" class="Field">arr</a> <a id="1163" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="1164" class="Symbol">)</a> <a id="1166" href="Relation.Binary.Reasoning.Base.Single.html#2555" class="Function Operator">∎</a>
    <a id="1172" class="Symbol">;</a> <a id="1174" href="Categories.Object.Monoid.html#903" class="Field">preserves-η</a> <a id="1186" class="Symbol">=</a> <a id="1188" href="Categories.Morphism.Reasoning.Core.html#1453" class="Function">pullʳ</a> <a id="1194" class="Symbol">(</a><a id="1195" href="Categories.Object.Monoid.html#903" class="Field">preserves-η</a> <a id="1207" href="Categories.Category.Construction.Monoids.html#824" class="Bound">g</a><a id="1208" class="Symbol">)</a> <a id="1210" href="Categories.Category.Core.html#3061" class="Function Operator">○</a> <a id="1212" href="Categories.Object.Monoid.html#903" class="Field">preserves-η</a> <a id="1224" href="Categories.Category.Construction.Monoids.html#822" class="Bound">f</a>
    <a id="1230" class="Symbol">}</a>
  <a id="1234" class="Symbol">;</a> <a id="1236" href="Categories.Category.Core.html#715" class="Field">assoc</a> <a id="1242" class="Symbol">=</a> <a id="1244" href="Categories.Category.Core.html#715" class="Function">assoc</a>
  <a id="1252" class="Symbol">;</a> <a id="1254" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a> <a id="1264" class="Symbol">=</a> <a id="1266" href="Categories.Category.Core.html#1004" class="Function">sym-assoc</a>
  <a id="1278" class="Symbol">;</a> <a id="1280" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1290" class="Symbol">=</a> <a id="1292" href="Categories.Category.Core.html#1096" class="Function">identityˡ</a>
  <a id="1304" class="Symbol">;</a> <a id="1306" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1316" class="Symbol">=</a> <a id="1318" href="Categories.Category.Core.html#1145" class="Function">identityʳ</a>
  <a id="1330" class="Symbol">;</a> <a id="1332" href="Categories.Category.Core.html#1339" class="Field">identity²</a> <a id="1342" class="Symbol">=</a> <a id="1344" href="Categories.Category.Core.html#1339" class="Function">identity²</a>
  <a id="1356" class="Comment">-- We cannot define equiv = equiv here, because _⇒_ of this category is a</a>
  <a id="1432" class="Comment">-- different level to the _⇒_ of 𝒞.</a>
  <a id="1470" class="Symbol">;</a> <a id="1472" href="Categories.Category.Core.html#1384" class="Field">equiv</a> <a id="1478" class="Symbol">=</a> <a id="1480" class="Keyword">record</a>
    <a id="1491" class="Symbol">{</a> <a id="1493" href="Relation.Binary.Structures.html#1568" class="Field">refl</a> <a id="1498" class="Symbol">=</a> <a id="1500" href="Relation.Binary.Structures.html#1568" class="Function">refl</a>
    <a id="1509" class="Symbol">;</a> <a id="1511" href="Relation.Binary.Structures.html#1594" class="Field">sym</a> <a id="1515" class="Symbol">=</a> <a id="1517" href="Relation.Binary.Structures.html#1594" class="Function">sym</a>
    <a id="1525" class="Symbol">;</a> <a id="1527" href="Relation.Binary.Structures.html#1620" class="Field">trans</a> <a id="1533" class="Symbol">=</a> <a id="1535" href="Relation.Binary.Structures.html#1620" class="Function">trans</a>
    <a id="1545" class="Symbol">}</a>
  <a id="1549" class="Symbol">;</a> <a id="1551" href="Categories.Category.Core.html#1438" class="Field">∘-resp-≈</a> <a id="1560" class="Symbol">=</a> <a id="1562" href="Categories.Category.Core.html#1438" class="Function">∘-resp-≈</a>
  <a id="1573" class="Symbol">}</a> <a id="1575" class="Keyword">where</a> <a id="1581" class="Keyword">open</a> <a id="1586" href="Categories.Category.Core.html#1530" class="Module">Equiv</a>
</pre></body></html>